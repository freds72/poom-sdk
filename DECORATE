actor GreenArmor : Armor 2018
{
  Radius 20
  Amount 50
  States
  {
  Spawn:
    ARM1 A -1
    Stop
  }
}

actor BlueArmor : Armor 2019
{
  Radius 20
  Amount 200
  States
  {
  Spawn:
    ARM1 A -1
    Stop
  }
}

actor HealthBonus : Health 2014
{
  Amount 1
  MaxAmount 200
  States
  {
  Spawn:
    BON1 A 6
	BON1 B 6
	BON1 C 6
	BON1 D 6 
	BON1 C 6
	BON1 B 6
    Loop
  }
}

actor ArmorBonus : Armor 2015
{
  Radius 20
  Height 16
  Amount 1
  Amount 200
  States
  {
  Spawn:
    BON2 A 6
	BON2 B 6
	BON2 C 6
	BON2 D 6
	BON2 C 6
	BON2 B 6
    Loop
  }
}


actor RedCard:Key 13
{
 SlotNumber 1
 HUDColor 12
 Icon 17
 States
 {
  Spawn:
   RKEY A -1
   Stop
 }
}

actor BlueCard:Key 5
{
 SlotNumber 2
 HUDColor 1
 Icon 17
 States
 {
  Spawn:	
   RKEY A -1
   Stop
 }
}

actor YellowCard:Key 6
{
 SlotNumber 3
 HUDColor 15
 Icon 17
 States
 {
  Spawn:
   GKEY A -1
   Stop
 }
}

actor Column 2028
{
 Radius 16
 Height 30
 +SOLID
 States
 {
	Spawn:
	 LIGH A -1
	 Stop
 }
}

actor Candelabra 35
{
 Radius 16
 Height 60
 +SOLID
 States
 {
	Spawn:
	 CBRA A -1 Bright
	 Stop
 }
}

actor DeadMarine 15
{
  States
  {
  Spawn:
    PLAY N -1
    Stop
  }
}

actor ExplosiveBarrel 2035
{
  Health 20
  Radius 10
  Height 42
  +SOLID
  +SHOOTABLE
  +NOBLOOD
  +DONTGIB

  States
  {
  Spawn:
    BAR1 A -1
    Stop
  Death:
    BAR1 A 10 Bright A_PlaySound(3)
    BEXP C 5 Bright
    BEXP D 5 Bright A_Explode(100, 128);
    BEXP E 10 Bright
    Stop
  }
}

actor BulletPuff 9999
{
  VSpeed 1
  +NOGRAVITY
  +RANDOMIZE
  States
  {
  Spawn:
    PUFF A 4 Bright
    PUFF B 4
    // Intentional fall-through
  Melee:
    PUFF C 4
		PUFF D 4
    Stop
	Pain:
		PUFB A 4 Bright
		PUFB B 4
  }
}

actor RocketSmokeTrail 5690
{
  +NOGRAVITY
  +RANDOMIZE
  VSpeed 1
  States
  {
  Spawn:
    PUFF C 4
    PUFF D 4
  }
}


actor PlasmaBall : Projectile 9998
{
	Radius 13
	Height 8
	Speed 25
 	Damage 5
	+RANDOMIZE

 States
	{
 	Spawn:
		PLSB A 120 Bright
		Stop
	Death:
		PLSE A 4 Bright
		PLSE C 4 Bright
		PLSE E 4 Bright
		Stop
	}
}

actor Rocket : Projectile 9997
{
  Radius 11
  Height 8
	Mass 8
  Speed 20
  Damage 20
	DeathSound 3
	TrailType "RocketSmokeTrail"
  +MISSILE

  States
  {
  Spawn:
    MISL A 120 Bright
	// blast on TTL
  Death:
    MISL B 8 Bright
    MISL C 6 Bright
    MISL D 4 Bright
    Stop
  }
}

actor Cell : Ammo 2047
{
	Amount 20
	MaxAmount 300
	// zap pico8 character
	Icon 150
	  States
	  {
	  Spawn:
		CELL A -1
		Stop
	  }
}

actor CellPack : Cell 17
{
  Amount 100
  States
  {
  Spawn:
    CELP A -1
    Stop
  }
}

actor RocketAmmo : Ammo 2010
{
	Amount 1
	MaxAmount 50
	// 
	Icon 26
	  States
  {
  Spawn:
    ROCK A -1
    Stop
  }
}

actor RocketBox : RocketAmmo 2046
{
  Amount 5
  States
  {
  Spawn:
    BROK A -1
    Stop
  }
}

actor Clip : Ammo 2007
{
	Amount 50
	MaxAmount 200
	Icon 152
  States
  {
  Spawn:
    CLIP A -1
    Stop
  }
}

actor ClipBox : Clip 2048
{
  Amount 50
  States
  {
  Spawn:
    AMMO A -1
    Stop
  }
}

actor Shell : Ammo 2008
{
	Amount 4
	MaxAmount 50
	Icon 153
	  States
	  {
	  Spawn:
		SHEL A -1
		Stop
	  }
}

actor ShellBox : Shell 2049
{
  Amount 20
  States
  {
  Spawn:
    SBOX A -1
    Stop
  }
}

actor PlasmaRifle : Weapon 2004
{
	AmmoUse 1
	AmmoGive 40
	AmmoType "Cell"
	SlotNumber 2
	HUDLabel "Plasma"
	States {
		Spawn:
			PLAS A 5
			PLAS A 5 Bright
			Loop
		Fire:
			PLSF A 2 A_Light(160)
			PLSF A 1 A_FireProjectile("PlasmaBall");
			PLSG B 5
			Goto Ready
		Ready:
			PLSG A 1 A_WeaponReady;
			Loop
	}
}

actor RocketLauncher : Weapon 2003
{
	AmmoUse 1
	AmmoGive 2
	AmmoType "RocketAmmo"
	SlotNumber 4
	States {
		Spawn:
			LAUN A 5
			LAUN A 5 Bright
			Loop
		Fire:
			MISG A 8 A_FireProjectile("Rocket");
			MISG B 12
			Goto Ready
		Ready:
			MISG A 1 A_WeaponReady;
			Loop
	}
}

actor ShotGun : Weapon 2001
{
	AmmoUse 1
	AmmoGive 8
	AmmoType "Shell"
	SlotNumber 1
	States {
		Spawn:
			SHOT A 5
			SHOT A 5 Bright
			Loop
		Fire:
			SHTG A 3 A_Light(160)
			SHTG A 5 A_FireBullets(2,0,3,3,"BulletPuff");
			SHTG B 20
			SHTG A 4
			Goto Ready
		Ready:
			SHTG A 1 A_WeaponReady;
			Loop
	}
}

actor ChainGun : Weapon 2002
{
	AmmoUse 1
	AmmoGive 20
	AmmoType "Clip"
	SlotNumber 3
	States {
		Spawn:
			MGUN A 5
			MGUN A 5 Bright
			Loop
		Fire:
			CHGG A 4 A_FireBullets(2,0,1,3,"BulletPuff");
			CHGG B 4
			Goto Ready
		Ready:
			CHGG A 1 A_WeaponReady;
			Loop
	}
}

actor Fist : Weapon 5000
{
	AmmoUse 0
	AmmoGive 0
	SlotNumber 5
	States {
		Ready:
			PUNG A 1 A_WeaponReady
			Loop
		Fire:
			PUNG B 4
			PUNG C 4 A_MeleeAttack(20,"BulletPuff")
			PUNG C 4
    	Goto Ready
	}
}

actor PoomGuy: PlayerPawn 1
{
	Radius 16
	Health 100
	Height 56
	Armor 50
	Speed 3
	StartItem "Shotgun"
	StartItem "Shell", 50
	StartItem "PlasmaRifle"
	StartItem "Cell", 100
	StartItem "Fist"
	+NOSECTORDMG
}

actor ZombieMan : Monster 3004
{
  Health 20
  Radius 20
  Height 56
  Speed 8
  PainChance 200
  // DropItem "Clip"
  States
  {
  Spawn:
    POSS A 10 A_Look;
    Loop
  See:
    POSS A 8
	POSS B 8 A_Chase;
    Loop
  Missile:
    POSS E 10 A_FaceTarget;
    POSS F 8 A_FireBullets(22.5, 0, 1, 9, "BulletPuff");
    POSS E 8
    Goto See
  Death:
    POSS H 5
    POSS I 5 // A_Scream
    POSS J 5 //A_NoBlocking
    POSS K 5
    POSS L 60
    Stop
  }
}

actor DoomImpBall: Projectile 9995
{
  Radius 6
  Height 8
  Speed 10
  FastSpeed 20
  Damage 3
  Mass 33
  +RANDOMIZE
  States
  {
  Spawn:
    BAL1 A 120 Bright
    Stop
  Death:
    BAL1 C 6 Bright
	BAL1 E 6 Bright
    Stop
  }
}

actor DoomImp: Monster 3001
{
	Health 60
	Radius 20
	Height 56
	Speed 8
	PainChance 200
	MeleeRange 512
	MaxTargetRange 800

	States
	{
		Spawn:
			TROO A 10 A_Look;
			Loop
		See:
			TROO A 3
			TROO B 2 
			TROO B 1 A_Chase;
			Loop
		Melee:
			TROO A 8 A_FaceTarget;
			TROO G 4 A_FireProjectile("PlasmaBall");
			Goto See
		Missile:
			TROO A 8 A_FaceTarget;
			TROO G 6 A_FireProjectile("DoomImpBall");
			Goto See
	  Death:
		TROO I 8
		TROO K 6
		TROO M 60
		Stop
	}
}

actor BaronOfHell: Monster 3003
{
	Health 1000
	Radius 24
	Height 64
	Mass 1000
	Speed 8
	PainChance 50
	
	States
	{
		Spawn:
			BOSS A -1
			Stop
	}
}

actor Cyberdemon: Monster 16
{
	Health 4000
	Radius 40
	Height 110
	Mass 1000
	Speed 16
	PainChance 20
	
	States
	{
		Spawn:
			CYBR A 4 A_FaceTarget(0.2);
			Loop
	}
}

actor Cacodemon: Monster 3005
{
	Health 400
	Radius 31
	Height 56
	Mass 400
	Speed 8
	PainChance 128
	
	States
	{
		Spawn:
			HEAD C -1
			Stop
	}
}

actor LostSoul: Monster 3006
{
  Health 100
  Radius 16
  Height 56
  Mass 50
  Speed 8
  Damage 3
  PainChance 256
  +FLOAT
  +NOGRAVITY
  +MISSILEMORE
  +DONTFALL
	+DROPOFF
  +NOICEDEATH

  States
  {
  Spawn:
    SKUL A 10 Bright A_Look;
    SKUL B 10 Bright A_Look;
    Loop
  See:
    SKUL A 6 Bright A_Chase;
    SKUL B 6 Bright A_Chase;
    Loop
  Missile:
    SKUL C 10 Bright A_FaceTarget;
  	SKUL D 4 Bright A_SkullAttack;
    SKUL C 4 Bright
    SKUL D 4 Bright
    Goto Missile
  Death:
    SKUL F 6 Bright
    SKUL G 6 Bright 
    SKUL H 6 Bright
    SKUL I 6 Bright 
    SKUL J 6
    SKUL K 6
    Stop
  }
}